def apiKeyPropertiesFile = rootProject.file("apiKey.properties")
def apiKeyProperties = new Properties()
apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'androidx.navigation.safeargs'

android {
    signingConfigs {
        config {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file('../-yusuf.keystore')
            storePassword STORE_PASSWORD
        }
    }
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    defaultConfig {
        applicationId "com.yoesuv.infomalangbatu"
        minSdkVersion 16
        targetSdkVersion 30
        multiDexEnabled true
        versionCode 12
        versionName "2.3.1"
        setProperty("archivesBaseName", applicationId+"-v"+versionCode+"("+versionName+")")
        resValue "string", "MAPS_API_KEY", apiKeyProperties['MAPS_API_KEY']
        resValue "string", "DIRECTION_API_KEY", apiKeyProperties['DIRECTION_API_KEY']
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res-menu',
                    'src/main/res-menu/listplace',
                    'src/main/res-menu/gallery',
                    'src/main/res-menu/maps',
                    'src/main/res-menu/other'
            ]
        }
    }
    flavorDimensions "default"
    productFlavors {
        development {
            buildConfigField "String", "BASE_URL", "\"https://info-malang-batu.firebaseapp.com/\""
        }
        production {
            buildConfigField "String", "BASE_URL", "\"https://info-malang-batu.firebaseapp.com/\""
        }
    }
    buildFeatures {
        dataBinding = true
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.13'
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "com.google.android.gms:play-services-maps:$playServicesMapsVersion"
    implementation "com.google.android.gms:play-services-location:$playServicesLocationVersion"
    implementation "com.google.firebase:firebase-perf:$firebasePerfVersion"
    implementation "com.google.firebase:firebase-core:$firebaseCoreVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$httpLoggingVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "com.intuit.ssp:ssp-android:$sspVersion"
    implementation "com.intuit.sdp:sdp-android:$sdpVersion"

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "com.github.GrenderG:Toasty:$toastyVersion"
    implementation "com.akexorcist:googledirectionlibrary:$googleDirectionLibraryVersion"
    implementation "com.android.support:multidex:$multidexVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"

    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
}

apply plugin: 'com.google.gms.google-services'
